resource "aws_dynamodb_table" "this" {
  name           = "${var.environment}-${var.table_name}"
  billing_mode   = var.billing_mode
  read_capacity  = var.billing_mode == "PROVISIONED" ? var.read_capacity : null
  write_capacity = var.billing_mode == "PROVISIONED" ? var.write_capacity : null
  hash_key       = var.hash_key
  range_key      = var.range_key

  dynamic "attribute" {
    for_each = var.attributes
    content {
      name = attribute.value.name
      type = attribute.value.type
    }
  }

  dynamic "global_secondary_index" {
    for_each = var.global_secondary_indexes
    content {
      name               = global_secondary_index.value.name
      hash_key           = global_secondary_index.value.hash_key
      range_key          = global_secondary_index.value.range_key
      projection_type    = global_secondary_index.value.projection_type
      read_capacity      = var.billing_mode == "PROVISIONED" ? global_secondary_index.value.read_capacity : null
      write_capacity     = var.billing_mode == "PROVISIONED" ? global_secondary_index.value.write_capacity : null
      non_key_attributes = global_secondary_index.value.non_key_attributes
    }
  }

  point_in_time_recovery {
    enabled = var.point_in_time_recovery_enabled
  }

  server_side_encryption {
    enabled = var.server_side_encryption_enabled
  }

  lifecycle {
    prevent_destroy = false
    ignore_changes = [
      read_capacity,
      write_capacity
    ]
  }

  tags = merge(
    var.tags,
    {
      Name        = "${var.environment}-${var.table_name}"
      Environment = var.environment
    }
  )
}